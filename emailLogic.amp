%%[

/* RETRIEVE FIELDS */
SET @fname = [FirstName]
SET @lname = [LastName]
SET @donorID = [DonorID]
SET @impactArea = [ImpactArea]


/* RETRIEVE PROJECT DATA */
SET @projectName = Lookup("Projects_Data", "ProjectName", "ImpactArea", @impactArea)
SET @projectStatus = Lookup("Projects_Data", "CompletionStatus", "ImpactArea", @impactArea)
SET @projectDesc = Lookup("Projects_Data", "ProjectDescription", "ImpactArea", @impactArea)
SET @projectCompDate = Lookup("Projects_Data", "CompletionDate", "ImpactArea", @impactArea)

SET @projectCompDateFormatted = FormatDate(@projectCompDate, "MMMM, YYYY")

/* FNAME AND LNAME CLEANUP */
IF EMPTY(@fname) THEN
  SET @finalName = "Valued Customer" 
  SET @subjectLine = "Thank You! Here's How Your Support is Making an Impact"
ELSE
  SET @finalFirstName = Propercase(Trim(@fname)) 
  SET @subjectLine = CONCAT("Thank You, ", @finalFirstName, "! Hereâ€™s How Your Support is Making an Impact")
  IF EMPTY(@lname) THEN
    SET @finalName = @finalFirstName
  ELSE
    SET @finalLastName = Propercase(Trim(@lname))
    SET @finalName = CONCAT(@finalFirstName, " ", @finalLastName)
  ENDIF
ENDIF

/* LOGIC TO CALCULATE TOTAL DONATION AMOUNT */
SET @donationTotal = 0

SET @rows = LookupRows("Donations","DonorID", @donorID)
SET @rowCount = rowcount(@rows)

IF  @rowCount > 0 THEN

  for @i = 1 to @rowCount do

    SET @row = row(@rows, @i) /* get row based on counter */
    SET @DonationAmount = field(@row,"DonationAmount")
    SET @donationTotal = Add(@donationTotal, @DonationAmount)
    
  next @i 

ELSE
  RaiseError("No donations found")
ENDIF

/* LOGIC FOR DYNAMIC DONATION COPY */
IF @donationTotal < 500 THEN
  SET @donationCopy = "We appreciate your support. Every bit of your contribution counts."

ELSEIF @donationTotal >= 500 AND @donationTotal < 1000 THEN
  SET @donationCopy = "As a token of our appreciation, we would like to invite you to an exclusive donor event."
  SET @URL = "https://www.greenearth.org/learn-more"
  SET @linkText = "Click here to learn more"

ELSEIF @donationTotal >= 1000 AND @donationTotal < 5000 THEN
  SET @donationCopy = "We have a special thank-you video message from our CEO just for you."
  SET @URL = "https://www.greenearth.org/thankyou-video"
  SET @linkText = "Watch it here"

ELSE
  SET @donationCopy = "We are honored to offer you a private tour of one of our conservation sites."
  SET @URL = "https://www.greenearth.org/schedule-tour"
  SET @linkText = "Schedule your visit here"
  

ENDIF

/* FORMAT TOTAL DONATION AMOUNT*/
SET @totalContributions = FormatNumber(@donationTotal, "C")

/* DYNAMIC IMAGE */
IF @impactArea == "Wildlife" THEN 
  SET @image = "http://image.greenearth.ca/lib/fe2f117371640475751078/m/1/e39eabd2-6719-40e3-beda-8f0a9e92433f.jpg"

ELSEIF @projecimpactAreatName == "Water" THEN 
  SET @image = "http://image.greenearth.ca/lib/fe2f117371640475751078/m/1/238749be-1ad1-4af5-b6a6-90b6fa44db95.jpeg"

ELSEIF @impactArea == "Energy" THEN
  SET @image = "http://image.greenearth.ca/lib/fe2f117371640475751078/m/1/735f1ad4-1f2d-4083-8d26-c8f5d4435037.jpg"

ELSE 
  SET @image = "http://image.greenearth.ca/lib/fe2f117371640475751078/m/1/b01ee012-5327-42ac-854b-e713dd8d7f50.jpg"

ENDIF

]%%



